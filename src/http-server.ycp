/**
 * File:	clients/http-server.ycp
 * Package:	Configuration of http-server
 * Summary:	Main file
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 *
 * Main file for http-server configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of the http-server</h3>
 */

textdomain "http-server";

import "CommandLine";
import "HttpServer";
import "Require";
import "Report";

include "commandline/commandline.ycp";

map cmdline_description = $[
    "id" 	: "http-server",
    // translators: command line help for HTTP server module
    "help"	: _("Configuration of HTTP server (Apache2)"),
    "guihandler"        : ``(HttpServerSequence()),
    "initialize"        : ``(HttpServer::Read()),
    "finish"            : ``(HttpServer::Write()),
    "actions"           : $[
	"configure"	: $[
	    // translators: help text for configure command line action
	    "help" 	: _("Configure host settings"),
	    "handler"	: ``(ConfigureHandler())
	],
	"modules"	: $[
	    // translators: help text for modules command line action
	    "help"	: _("Configure the Apache2 server modules"),
	    "handler"	: ``(ModulesHandler())
	],
	"listen"	: $[
	    // translators: help text for listen command line action
	    "help" 	: _("Set up the ports and network addresses where the server should listen."),
	    "handler"	: ``(ListenHandler())
	],
    ],
    "options"		: $[
	"servername"	: $[
	    "type"	: "hostname",
	    // translators: help text for servername option (configure command line action)
	    "help"	: _("Server name, for example, www.example.com")
	],
	"serveradmin"	: $[
	    "type"	: "email",
	    // translators: help text for serveradmin option (configure command line action)
	    "help"	: _("E-mail address of the server administrator")
	],
	"documentroot"	: $[
	    "type"	: "string",
	    // translators: help text for documentroot option (configure command line action)
	    "help"	: _("Directory where the documents of the server are stored")
	],
	"host"		: $[
	    "type"	: "string",
	    // translators: help text for host option (configure command line action)
	    "help"	: _("Name of the host to configure. Only \"defaulthost\" is valid at the moment.")
	],
	"add"		: $[
	    "type"	: "regex",
	    "typespec"	: "(\[[0-9a-f:]+\]:[0-9]+)|([0-9]+)|([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)",
	    // translators: help text for add subcommand (listen command line action)
	    "help"	: _("Add a new listen entry ([address:]port)"),
	],
	"delete"	: $[
	    "type"	: "regex",
	    "typespec"	: "(\[[0-9a-f:]+\]:[0-9]+)|([0-9]+)|([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)",
	    // translators: help text for delete subcommand (listen command line action)
	    "help"	: _("Delete an existing listen entry ([address:]port)"),
	],
	"list"		: $[
	    // translators: help text for list subcommand (listen command line action)
	    "help"	: _("List configured entries"),
	],
	"enable"	: $[
	    "type"	: "regex",
	    "typespec"	: ".+(,.+)*",
	    // translators: help text for enable subcommand (modules command line action)
	    "help"	: _("Comma-separated list of modules to enable")
	],
	"disable"	: $[
	    "type"	: "regex",
	    "typespec"	: ".+(,.+)*",
	    // translators: help text for disable subcommand (modules command line action)
	    "help"	: _("Comma-separated list of modules to disable")
	]
    ],
    "mappings"		: $[
	"configure"	: [ "host", "servername", "serveradmin", "documentroot" ],
	"modules"	: [ "enable", "disable", "list" ],
	"listen"	: [ "add", "delete", "list" ]
    ]
];

define boolean ConfigureHandler(map options) ``{

    if( options["host"]:nil != "defaulthost" ) {
	if( ! haskey( options, "host" ) ) {
	    // translators: error message in configure command line action
	    Report::Error( _("Configured host not specified") );
	}
	else 
	{
	    // translators: error message in configure command line action
	    Report::Error( _("Only \"defaulthost\" can be specified as the host to configure") );
	}
	return false;
    }

    string host = options["host"]:nil;
    
    value = options["servername"]:nil;
    if( value != nil ) {
	HttpServer::settings[host, "servername"] = value;
	HttpServer::modified = true;
    }

    value = options["serveradmin"]:nil;
    if( value != nil ) {
	HttpServer::settings[host, "serveradmin"] = value;
	HttpServer::modified = true;
    }

    value = options["documentroot"]:nil;
    if( value != nil ) {
	HttpServer::settings[host, "documentroot"] = value;
	HttpServer::modified = true;
    }

    return true;
}

define boolean ModulesHandler(map options) ``{
    // check the command to be present exactly once
    string command = CommandLine::UniqueOption( options,
        ["enable", "disable", "list" ] );
    if( command == nil ) return false;

    if( command == "enable" ) {
	list mods = splitstring( options["enable"]:"", "," );
	
	foreach( string mod, mods, ``{ HttpServer::settings["modules",mod] = true; });
	
	HttpServer::modules_modified = true;
	HttpServer::modified = true;
    }
    else if( command == "disable" ) {
	list mods = splitstring( options["disable"]:"", "," );
	
	foreach( string mod, mods, ``{ HttpServer::settings["modules",mod] = false; });

	HttpServer::modules_modified = true;
	HttpServer::modified = true;
    }
    else if( command == "list" ) {
        // translators: heading for the "modules list" command line action output
	// please, try to align the texts if possible.
	CommandLine::Print( _("Status \tModule
=================="));
	
	foreach( string mod, boolean status, HttpServer::settings["modules"]:[], ``{
	    // translators: status of a module
	    CommandLine::Print( sformat( "%1\t%2", status ? _("Enabled") : _("Disabled"), mod ) );
	});
    }

    return true;
}

define boolean ListenHandler(map options) ``{

    // check the command to be present exactly once
    string command = CommandLine::UniqueOption( options,
        ["add", "delete", "list" ] );
    if( command == nil ) return false;
    
    if( command == "add" ) {
	if( contains( HttpServer::settings["defaulthost", "Listen"]:[], options["add"]:"" ) )
	{
	    // translators: error message in "listen add" command line action
	    Report::Error( sformat( _("The listen statement '%1' is already configured.") , options["add"]:"") );
	    return false;
	}
	
	HttpServer::settings["defaulthost", "Listen"] = 
	    add( HttpServer::settings["defaulthost", "Listen"]:[], options["add"]:"" );
	HttpServer::modified = true;
    }
    else if( command == "delete" ) {
	if( !contains( HttpServer::settings["defaulthost", "Listen"]:[], options["delete"]:"" ) )
	{
	    // translators: error message in "listen delete" command line action
	    Report::Error( sformat( _("Cannot delete unconfigured listen statement '%1'") , options["delete"]:"") );
	    return false;
	}

	HttpServer::settings["defaulthost", "Listen"] = 
	    filter( string value, HttpServer::settings["defaulthost", "Listen"]:[], ``( value != options["delete"]:"" ) );
	HttpServer::modified = true;
    }
    else if( command == "list" ) {
        // translators: heading for the "listen list" command line action output
	// please, try to align the texts if possible.
	CommandLine::Print( _("Listen Statements:
==================
"));
	
	foreach( string listen, HttpServer::settings["defaulthost", "Listen"]:[], ``{
	    if( issubstring( listen, ":" ) ) {
		CommandLine::Print( listen );
	    }
	    else
	    {
		// translators: listen statement for all available interfaces
		CommandLine::Print( sformat( _("All Interfaces: %1"), listen ) );
	    }
	});

    }

    return true;
}

/* The main () */
y2milestone ("----------------------------------------");
y2milestone ("HttpServer module started");

include "http-server/wizards.ycp";

/* is this proposal or not? */
boolean propose = false;
list args = WFM::Args();
if(size(args) > 0) {
    if(is(WFM::Args(0), path) && WFM::Args(0) == .propose) {
        y2milestone("Using PROPOSE mode");
        propose = true;
    }
}

// check rpms
if( !Require::RequireAndConflictTarget( ["apache2"], [],
    // notification about package needed 1/2
    _("<p>To configure the HTTP server, the <b>%1</b> package must be installed.</p>") +
    // notification about package needed 2/2
    _("<p>Do you want to install it now?</p>")) )
{
    return false;
}

/* main ui function */
any ret = nil;

if(propose) ret = HttpServerAutoSequence();
else ret = CommandLineRun(cmdline_description);
y2debug("ret=%1", ret);

/* Finish */
y2milestone("HttpServer module finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
